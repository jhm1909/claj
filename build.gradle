import com.xpdustry.toxopid.spec.ModMetadata
import com.xpdustry.toxopid.spec.ModPlatform
//import com.xpdustry.toxopid.extension.anukeJitpack
//import com.xpdustry.toxopid.extension.anukeXpdustry

plugins {
  id "java"
  id "com.xpdustry.toxopid" version "4.1.0"
}

def metadata = ModMetadata.fromJson(file("mod.hjson"))

allprojects {
  group = "com.xpdustry"
  version = metadata.version
  description = metadata.description
  
  toxopid.compileVersion = "v${metadata.minGameVersion}"
  
  repositories {
    mavenCentral()
    maven { url "https://maven.xpdustry.com/mindustry" }//anukeXpdustry()
    maven { url "https://www.jitpack.io" }//anukeJitpack()
  }
  
  dependencies {
    // I realy want to use it....
    //compileOnly "com.github.bsideup:jabel:1.0.1"
    //annotationProcessor "com.github.bsideup:jabel:1.0.1"
  }

  tasks.withType(JavaCompile) {
    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8 //JavaVersion.VERSION_17
    options.encoding = "UTF-8"
    
    //options.compilerArgs.addAll(['--release', '8'])
  }
}

project(":client") {
  apply plugin: "java"
  apply plugin: "com.xpdustry.toxopid"

  sourceSets.main.java.srcDirs = ["src"]
  sourceSets.main.resources.srcDirs = ["resources"]
  
  toxopid.platforms = [ModPlatform.DESKTOP, ModPlatform.ANDROID]
  
	dependencies {
	  compileOnly toxopid.dependencies.arcCore
    compileOnly toxopid.dependencies.mindustryCore
	}

  mergeJar {
    archiveFileName = "${metadata.name}-client.jar"
   
    // Includes mod descriptor
    from(rootDir) {
      include "mod.hjson"
    }
    
    //copy the builded jar to the working directory
    doLast {
      copy {
        from mergeJar
        into rootDir
      }
    }
  }
	
	// Includes android dex
  build {
    dependsOn mergeJar
  }
}

project(":server") {
  apply plugin: "java"
  apply plugin: "com.xpdustry.toxopid"
  

  sourceSets.main.java.srcDirs = ["src"]
  sourceSets.main.resources.srcDirs = ["resources"]
  
  toxopid.platforms = [ModPlatform.SERVER]

  dependencies {
    implementation toxopid.dependencies.arcCore
    implementation "com.github.Anuken.Arc:arcnet:${toxopid.compileVersion}"
    //implementation toxopid.dependencies.arcHeadless
  }
  
  jar {
    archiveFileName = "${metadata.name}-server.jar"
    manifest.attributes["Main-Class"] = "com.xpdustry.claj.server.Main"
    
    // Includes arc libs
    from {
      configurations.runtimeClasspath.collect {
        it.isDirectory() ? it : zipTree(it)
      }
    }
    
    //copy the builded jar to the working directory
    doLast {
      copy {
        from jar
        into rootDir
      }
    }
  }
}
