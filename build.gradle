import com.xpdustry.toxopid.spec.ModMetadata
import com.xpdustry.toxopid.spec.ModPlatform
//import com.xpdustry.toxopid.extension.anukeJitpack
//import com.xpdustry.toxopid.extension.anukeXpdustry

plugins {
  id "java"
  id "com.xpdustry.toxopid" version "4.1.2"
}

def metadata = ModMetadata.fromJson(file("mod.hjson"))

allprojects {
  group = "com.xpdustry"
  version = metadata.version
  description = metadata.description
  
  toxopid.compileVersion = "v${metadata.minGameVersion}"
  
  repositories {
    mavenCentral()
    maven { url "https://maven.xpdustry.com/mindustry" }//anukeXpdustry()
    maven { url "https://www.jitpack.io" }//anukeJitpack()
  }
  
  dependencies {
    // I realy want to use it....
    //compileOnly "com.github.bsideup:jabel:1.0.1"
    //annotationProcessor "com.github.bsideup:jabel:1.0.1"
  }

  tasks.withType(JavaCompile) {
    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8 //JavaVersion.VERSION_17
    options.encoding = "UTF-8"
    options.compilerArgs.addAll(['--release', '8'])
  }
}

project(":client") {
  apply plugin: "java"
  apply plugin: "com.xpdustry.toxopid"

  sourceSets.main.java.srcDirs = ["src"]
  sourceSets.main.resources.srcDirs = ["resources"]
  
  toxopid.platforms = [ModPlatform.DESKTOP, ModPlatform.ANDROID]
  
  dependencies {
    compileOnly toxopid.dependencies.arcCore
    compileOnly toxopid.dependencies.mindustryCore
  }

  mergeJar {
    archiveFileName = "${metadata.name}-${project.name}.jar"
   
    // Includes mod json and icon
    from(rootDir) {
      include "mod.hjson"
      include "icon.png"
    }

    // Copy the builded jar to the working directory
    doLast {
      copy {
        from mergeJar
        into rootDir
      }
    }
  }
	
  // Includes android dex
  build {
    dependsOn mergeJar
  }
}

project(":server") {
  apply plugin: "java"
  apply plugin: "com.xpdustry.toxopid"
  
  sourceSets.main.java.srcDirs = ["src"]
  sourceSets.main.resources.srcDirs = ["resources"]
  
  project.ext.mainClassName = "com.xpdustry.claj.server.Main"
  toxopid.platforms = [ModPlatform.SERVER]

  dependencies {
    implementation toxopid.dependencies.arcCore
    implementation "com.github.Anuken.Arc:arcnet:${toxopid.compileVersion}" //toxopid.dependencies.arcnet
    //implementation toxopid.dependencies.arcHeadless
  }
  
  jar {
    archiveFileName = "${metadata.name}-${project.name}.jar"
    manifest.attributes["Main-Class"] = project.ext.mainClassName
    manifest.attributes["Claj-Version"] = metadata.version
    
    // Includes arc libs
    from {
      configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    
    // Copy the builded jar to the working directory
    doLast {
      copy {
        from jar
        into rootDir
      }
    }
  }
  
  task run(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = "build"
    args 7000 // default port
    jvmArgs "-DClaj-Version=${metadata.version}" // claj version
  }
}
